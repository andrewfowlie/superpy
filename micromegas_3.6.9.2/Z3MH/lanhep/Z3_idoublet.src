%
% Standard Model - unitary and  tHooft-Feynman gauges.
%

option chepPDWidth=181.
keys gauge_fixing=Feynman.

model 'Z3 Inert Dublet'/1.
prtcformat fullname:
'Full   Name     ', name:' P ', aname:' aP', pdg:'  number  ',
spin2,mass,width, color, aux, texname: '> LaTeX(A)       <',
atexname:'>  LateX(A+)           <' .



option ReduceGamma5=0.
let g5=gamma5.
use sm_tex.

parameter  EE  = 0.31223 : 'Electromagnetic coupling constant (<->1/128)',
	   GG  = 1.117   : 'Strong coupling constant (Z point)  (PDG-94)',
       alfSMZ=0.1184 : 'alpha_QCD(MZ)',
	   SW  = 0.481  : 'sin of the Weinberg angle (MS-bar)'.

parameter  CW  = sqrt(1-SW**2) : 'cos of the Weinberg angle'.
parameter  MZ=91.187.
parameter  MW=MZ*CW.
parameter  Q=100.

parameter Mtp   =173.5   : 'top quark pole mass',
MbMb  =4.23   : 'Mb(Mb) running mass',
McMc  =1.27    : 'Mc(Mc) running mass'.


external_func(initQCD5,4).
external_func(McRun,1).
external_func(MbRun,1).
external_func(MtRun,1).
external_func(alphaQCD,1).
external_func(McEff,1).
external_func(MbEff,1).
external_func(MtEff,1).

parameter Lqcd=initQCD5(alfSMZ,McMc,MbMb,Mtp).
parameter Mb=MbEff(Q).
parameter Mt=MtEff(Q).
parameter Mc=McEff(Q).
parameter PI=acos(-1).
parameter Mbp=MbMb*(1+4/3*alphaQCD(MbMb)/PI).
parameter Mcp=Mc*(1+4/3*alphaQCD(Mc)/PI). 




parameter V=2*MW/EE*SW.



let Vud=1,Vus=0,Vub=0,Vcd=0,Vcs=1,Vcb=0,Vtd=0,Vts=0,Vtb=1.




vector  
	A/A: (photon, pdg 22, gauge),
	Z/Z:('Z boson',pdg 23, mass MZ , width wZ = 2.502, gauge),
	G/G: (gluon, pdg 21,color c8, gauge),
	'W+'/'W-': ('W boson',pdg 24, mass MW, width wW = 2.094, gauge).

spinor 		ne/Ne:(neutrino,pdg 12,left), 	  e:(electron,pdg 11),
		nm/Nm:('mu-neutrino',pdg 14,left),   m:(muon, pdg 13,mass Mm  = 0.1057),
		nl/Nl:('tau-neutrino',pdg 16,left),  l:('tau-lepton',pdg 15, mass Ml  = 1.777).

spinor		u:('u-quark',pdg 2,color c3, mass Mu=0.01),
		d:('d-quark',pdg 1,color c3, mass Md=0.01),
		c:('c-quark',pdg 4,color c3, mass Mc ),
		s:('s-quark',pdg 3,color c3, mass Ms = 0.200),
		t:('t-quark',pdg 6,color c3, mass Mt , width wtop = 1.442),
		b:('b-quark',pdg 5,color c3, mass Mb ).


scalar h/h:('Higgs', pdg 25, mass Mh = 125, width wh = auto).

			
let l1={ne,e},  L1={Ne,E}.
let l2={nm,m},  L2={Nm,M}.
let l3={nl,l},  L3={Nl,L}.

let q1={u,d}, Q1={U,D}, q1a={u,Vud*d+Vus*s+Vub*b}, Q1a={U,Vud*D+Vus*S+Vub*B}.
let q2={c,s}, Q2={C,S}, q2a={c,Vcd*d+Vcs*s+Vcb*b}, Q2a={C,Vcd*D+Vcs*S+Vcb*B}. 
let q3={t,b}, Q3={T,B}, q3a={t,Vtd*d+Vts*s+Vtb*b}, Q3a={T,Vtd*D+Vts*S+Vtb*B}.

let B1= -SW*Z+CW*A, W3=CW*Z+SW*A, W1=('W+'+'W-')/Sqrt2,
	 W2 = i*('W+'-'W-')/Sqrt2.


let gh1 = ('W+.c'+'W-.c')/Sqrt2, gh2= i*('W+.c'-'W-.c')/Sqrt2,
		gh3= CW*'Z.c'+SW*'A.c', gh={gh1,gh2,gh3}.

let Gh1 = ('W+.C'+'W-.C')/Sqrt2, Gh2=i*('W+.C'-'W-.C')/Sqrt2, 
		Gh3= CW*'Z.C'+SW*'A.C', Gh={Gh1,Gh2,Gh3}. 


let WW1 = {W1,  W2 , W3}, WW = {'W+',W3,'W-'}.

let g=EE/SW, g1=EE/CW.


% Self-interaction of gauge bosons



lterm -F**2/4   where 
	F=deriv^mu*B1^nu-deriv^nu*B1^mu.

lterm -F**2/4  where
	F=deriv^mu*G^nu^a-deriv^nu*G^mu^a+i*GG*f_SU3^a^b^c*G^mu^b*G^nu^c.

lterm -F**2/4  where
F=deriv^mu*WW1^nu^a-deriv^nu*WW1^mu^a -g*eps^a^b^c*WW1^mu^b*WW1^nu^c.




% left fermion interaction with gauge fields


lterm  	anti(psi)*gamma*(1-g5)/2*(i*deriv-g*taupm*WW/2-Y*g1*B1)*psi
		where 
			psi=l1,  Y=-1/2;
			psi=l2,  Y=-1/2;
			psi=l3,  Y=-1/2;
			psi=q1a, Y= 1/6;
			psi=q2a, Y= 1/6;
			psi=q3a, Y= 1/6.

% right fermion interaction with gauge fields

lterm  	anti(psi)*gamma*(1+g5)/2*(i*deriv - Y*g1*B1)*psi
		where 
			psi=e,Y= -1;
			psi=m,Y= -1;
			psi=l,Y= -1;
			psi=u, Y=  2/3;
			psi=c, Y=  2/3;
			psi=t, Y=  2/3;
			psi=d, Y= -1/3;
			psi=s, Y= -1/3;
			psi=b, Y= -1/3.


% quark-gluon interaction

lterm  GG*anti(psi)*lambda*gamma*G*psi where
	psi=q1; psi=q2; psi=q3.




let hdbl = { -i*'W+.f',  (vev((2*MW/EE*SW))+h+i*'Z.f')/Sqrt2 }, 
    Hdbl = {  i*'W-.f',  (vev((2*MW/EE*SW))+h-i*'Z.f')/Sqrt2 }.




lterm  -M/MW/Sqrt2*g*(anti(pl)*(1+g5)/2*pr*hdbl + anti(pr)*(1-g5)/2*pl*Hdbl )
    where
	M=Vud*Md,  pl=q1a, pr=d;          % 0 stands for Md 
	M=Vus*Ms, pl=q1a, pr=s;
	M=Vub*Mb, pl=q1a, pr=b;
	M=Vcd*0,  pl=q2a, pr=d;
	M=Vcs*Ms, pl=q2a, pr=s;
	M=Vcb*Mb, pl=q2a, pr=b;
	M=Vtd*0,  pl=q3a, pr=d;
	M=Vts*Ms, pl=q3a, pr=s;
	M=Vtb*Mb, pl=q3a, pr=b.


lterm  -M/MW/Sqrt2*g*(anti(pl)*(1+g5)/2*i*tau2*pr*Hdbl 
		+ anti(pr)*(1-g5)/2*i*pl*tau2*hdbl ) 
 where
	M=Mu,  pl=q1a, pr=u;
	M=Mc,  pl=q2a, pr=c;
	M=Mt,  pl=q3a, pr=t.

lterm  -M/MW/Sqrt2*g*(anti(pl)*(1+g5)/2*pr*hdbl + anti(pr)*(1-g5)/2*pl*Hdbl )
    where
	M=Mm,  pl=l2,  pr=m;
	M=Ml,  pl=l3,  pr=l.
	


lterm -2*lambda*(hdbl*Hdbl-(2*MW/EE*SW)**2/2)**2  where 
	lambda=(g*Mh/MW)**2/16.



let Dhdbl^mu^a = (deriv^mu+i*g1/2*B1^mu)*hdbl^a +
	 i*g/2*taupm^a^b^c*WW^mu^c*hdbl^b.

let DHdbl^mu^a = (deriv^mu-i*g1/2*B1^mu)*Hdbl^a 
	-i*g/2*taupm^a^b^c*{'W-'^mu,W3^mu,'W+'^mu}^c*Hdbl^b.

	

lterm DHdbl*Dhdbl.



lterm -i*GG*f_SU3*ccghost(G)*G^mu*deriv^mu*ghost(G).
lterm  -1/2*(deriv*G)**2.


%lterm -g*eps*gh*WW1*deriv*Gh.

lterm g*eps*deriv*Gh*gh*WW1.


lterm  -1/2*(deriv*A)**2.


lterm  -1/2*(2*(deriv*'W+'+MW*'W+.f')*(deriv*'W-'+MW*'W-.f') +
	(deriv*Z+MW/CW*'Z.f')**2).


lterm -MW*EE/2/SW*((h+i*'Z.f')*('W-.C'*'W+.c' + 'W+.C'*'W-.c')
    		+h*'Z.C'*'Z.c'/CW**2-2*i*'Z.f'*'W+.C'*'W-.c').

lterm i*EE*MW/2/CW/SW*(
	'W+.f'*('W-.C'*'Z.c'*(1-2*SW**2)+'W-.c'*'Z.C'
			+2*CW*SW*'W-.C'*'A.c') -
	'W-.f'*('W+.C'*'Z.c'*(1-2*SW**2)+'W+.c'*'Z.C'
			+2*CW*SW*'W+.C'*'A.c')).

% ===============  New Parameters =================
parameter  la3=0.2, la2=0.01, laS=0.1, laS1=0.1, laS2=0.1, laS21=0.1  .
parameter Mdm1=50.0, Mdm2=200, sinDm=0.1, MHC=200 .
parameter muppS=100 .

% ==============  New Constraints =====================
parameter cosDm=sqrt(1-sinDm**2).
parameter muSq  = ( Mdm2**2*sinDm**2 + Mdm1**2*cosDm**2 - V**2*laS1/2).  
parameter mu_sh = -4*(Mdm2**2 -Mdm1**2)*sinDm*cosDm/V/Sqrt2.
parameter mu2q= MHC**2 -la3*V**2/2.
parameter la4=  2*( Mdm1**2*sinDm**2 + Mdm2**2*cosDm**2 - MHC**2)/V**2.  
%================= New Particles =====================

scalar '~H+'/'~H-':('Charged Higgs',pdg 37, mass MHC,  width wHC=auto).
scalar '~x1'/'~X1':('second Higgs', pdg 35, mass Mdm1, width wX1=auto).
scalar '~x2'/'~X2':('second Higgs', pdg 45, mass Mdm2, width wX2=auto).

prtcproperty aux:('~x1'='0').

let idbl = { -i*'~H+',  sinDm*'~x1' +cosDm*'~x2' }, 
    Idbl = {  i*'~H-',  sinDm*'~X1' +cosDm*'~X2' }.

let sc=            cosDm*'~x1'  - sinDm*'~x2'.
let Sc=            cosDm*'~X1'  - sinDm*'~X2'.

%=================== New Kinetic terms  ==================

let Didbl^mu^a = (deriv^mu+i*g1/2*B1^mu)*idbl^a +
	 i*g/2*taupm^a^b^c*WW^mu^c*idbl^b.


let DIdbl^mu^a = (deriv^mu-i*g1/2*B1^mu)*Idbl^a 
	-i*g/2*taupm^a^b^c*{'W-'^mu,W3^mu,'W+'^mu}^c*Idbl^b.	



let Dsc^mu = (deriv^mu)*sc.
let DSc^mu = (deriv^mu)*Sc.

lterm Dsc*DSc.

lterm DIdbl*Didbl.

%================== New Vertexes ====================

lterm -mu2q*idbl*Idbl.

lterm -la2*(idbl*Idbl)**2.
lterm -la3*(hdbl*Hdbl)*(idbl*Idbl).
lterm -la4*(hdbl*Idbl)*(Hdbl*idbl).

lterm -laS*(Sc*sc)*(Sc*sc).
lterm -muSq*(Sc*sc).
lterm -laS1*(Sc*sc)*(hdbl*Hdbl) -laS2*(Sc*sc)*(idbl*Idbl).

lterm  - muppS*(sc*sc*sc+Sc*Sc*Sc)/2.
lterm  - laS21*((Sc*Sc)*(Idbl*hdbl) +(sc*sc)*(idbl*Hdbl))/2.
lterm  - mu_sh*(Sc*(Hdbl*idbl)+sc*(Idbl*hdbl))/2.



SetAngle(1-SW**2=CW**2).
SetAngle(MW/CW=MZ).
read 'higgs_eff.src'.

CheckMasses.
CheckHerm.
